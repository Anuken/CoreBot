apply plugin: 'java'

project.ext.mainClassName = "corebot.CoreBot"
sourceCompatibility = targetCompatibility = 16
version = '1.0'

sourceSets.main.java.srcDirs = ["src/"]
allprojects{
    repositories{
        mavenLocal()
        mavenCentral()
        maven{ url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven{ url "https://oss.sonatype.org/content/repositories/releases/" }
        maven{ url 'https://jitpack.io' }
        maven{
            name 'm2-dv8tion'
            url 'https://m2.dv8tion.net/releases'
        }
    }
}

ext{
    //currently unused
    mindustryVersion = 'v126'
}

task run(dependsOn: classes, type: JavaExec){
    if(project.hasProperty("token")) systemProperty "token", project.getProperty("token")
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}

task dist(type: Jar){
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.runtimeClasspath.collect{ it.isDirectory() || !it.name.endsWithAny(".zip", ".jar") ? it : zipTree(it) }}

    archiveFileName = "CoreBot.jar"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest{
        attributes 'Main-Class': project.mainClassName
    }
}

dependencies{
    implementation "net.dv8tion:JDA:4.2.1_269"
    implementation "org.jsoup:jsoup:1.7.2"
    implementation "com.github.Anuken.Arc:arc-core:45d20b5fc3edac3da0c2497f11ae30a0d10e7e20"
    implementation "com.github.Anuken.Mindustry:core:57fd72f476"
    implementation "com.github.twitch4j:twitch4j:1.2.1"
}
