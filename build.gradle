apply plugin: 'java'
apply plugin: 'maven'

project.ext.mainClassName = "corebot.CoreBot"
sourceCompatibility = 1.8
version = '1.0'

sourceSets.main.java.srcDirs = ["src/"]
allprojects{
    repositories{
        mavenLocal()
        mavenCentral()
        maven{ url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven{ url "https://oss.sonatype.org/content/repositories/releases/" }
        maven{ url 'https://jitpack.io' }
        jcenter()
    }
}

ext{
    mindustryVersion = 'v113'
}

task run(dependsOn: classes, type: JavaExec){
    if(project.hasProperty("token")) systemProperty "token", project.getProperty("token")
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}

task dist(type: Jar){
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.runtimeClasspath.collect{ it.isDirectory() || !it.name.endsWithAny(".zip", ".jar") ? it : zipTree(it) }}

    archiveFileName = "CoreBot.jar"

    manifest{
        attributes 'Main-Class': project.mainClassName
    }
}

dependencies{
    compile 'net.dv8tion:JDA:4.2.0_211'
    compile "org.jsoup:jsoup:1.7.2"
    compile "com.github.Anuken.Arc:arc-core:b99073959675a037f3a722a7ca08d28c21e10038"
    compile "com.github.Anuken.Mindustry:core:7bb23c33841e84182c05f25290e0c556bc4f7685"
}
